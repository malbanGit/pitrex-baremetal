	.TITLE	WSPANL	PANEL ON WALLS OF TRENCH OF BASE STAR
	.INCLUDE	WSCOMN	;<<<<<<<<<<<<<<<<<<<<<<<<<<<<
				;===
				;---
M$AX	==-10+0
M$AY	==-10+0+2
M$AZ	==-10+0+0+4

M$BX	==-10+0+8
M$BY	==-10+0+8+2
M$BZ	==-10+0+8+4

M$CX	==-10+0+10
M$CY	==-10+0+10+2
M$CZ	==-10+0+10+4

M$TX	==-10+18+0
M$TY	==-10+18+2
M$TZ	==-10+18+4

M$VX	==-10+6
M$VY	==-10+6+8
M$VZ	==-10+6+10

				;===
				;D-PAGE RAM
	.GLOBB BJ.ID
	.GLOBB PN.DES,PN.FLG,PN.ID
	.GLOBB S.GAS,S.ROL
	.GLOBB S2.PRM
				;---
				;RAM
	.GLOBL PNLW,PNLWZ
	.GLOBB PNLW$IZ
	.GLOBL PNRW,PNRWZ
	.GLOBB PNRW$IZ
	.GLOBL WG.FCB,WG.FCC,WG.FCU,WG.FCZ
				;---
				;OUTSIDE ROUTINES
	.GLOBL AUDCR
	.GLOBL BG1GLW
	.GLOBL BJFROT,BJGROT,BJGPNT,BJBCLP,BJGDRW
	.GLOBL MGOWT
	.GLOBL TD$WPN,TD$WFF,TD$WFG,TD$WGA
				;---
				;MATH BOX
	.GLOBL M.BIC
	.GLOBL M.U1
	.GLOBL M.X0,M.Y0,M.Z0,M$X0BC
	.GLOBL M.XP
	.GLOBB M$PSB2
				;---
				;VECTOR GENERATOR
	.GLOBL VGCTRQ,VGCPRP,VGCYLW,VGCOPC
				;---
				;HARDWARE
	.GLOBL P.RND1
				;---
				;SHIFT ROUTINES
				;===

	.MACRO .PAGG		;GLOBAL PAGE NUMBERS
	.PAGEG P		;PANELS ON BASESTAR WALLS
	.ENDM

	.CSECT PANL

	.PAGG
	.SBTTL TABLES

	.SBTTL BASESTAR PANELS -- VWPANEL,PNVLW

VWPANEL::			;VIEW THE PANELS
	LDA WG.FCU		;VERIFY CORRECT FORCE FIELD CUE VALUE
	CMPA #ZFFCUE-TFFCUE/2
	IFHS			;?WRAPPED OVER TOP?
	LDA #0			;THEN START FRESH
	STA WG.FCU
	ENDIF
				;---
	LDA #1
	STA PN.DES		;DESIRED TYPE OF PANEL TO SEE
				;---
	LDA #TD$WPN		;DECORATIVE WALL PANEL
	STA BJ.ID
	STA PN.ID
	JSR BJGROT		;PRE-ROTATE ITS POINTS
	JSR PNVLW		;VIEW LEFT WALL PANELS
				;---
	LDA PN.ID
	STA BJ.ID
	JSR BJFROT		;FLIPPED PRE-ROTATE
	JSR PNVRW		;VIEW RIGHT WALL PANELS
				;---
	LDA #2
	STA PN.DES		;DESIRED TYPE OF PANEL TO SEE
				;---
	LDA #TD$WFF		;WALL FORCE FIELD
	STA BJ.ID
	STA PN.ID
	JSR BJGROT		;PRE-ROTATE ITS POINTS
	JSR PNVLW		;VIEW LEFT WALL PANELS
				;---
	LDA PN.ID
	STA BJ.ID
	JSR BJFROT		;FLIPPED PRE-ROTATE
	JSR PNVRW		;VIEW RIGHT WALL PANELS
				;---
	LDA #3
	STA PN.DES		;DESIRED TYPE OF PANEL TO SEE
				;---
	LDA #TD$WGA		;WALL GUN TYPE A
	STA BJ.ID
	STA PN.ID
	JSR BJGROT		;PRE-ROTATE ITS POINTS
	JSR PNVLW		;VIEW LEFT WALL PANELS
				;---
	LDA PN.ID
	STA BJ.ID
	JSR BJFROT		;FLIPPED PRE-ROTATE
	JSR PNVRW		;VIEW RIGHT BOTTOM PANELS
				;---
	RTS
				;===

PNVLW:				;VIEW THE LEFT PANELS
	LDA #0
	STA WG.FCB		;NO BUMP TO NEXT FORCE FIELD CUE YET
	LDA WG.FCU		;INIT CURRENT CUE COLOR
	STA WG.FCC
	LDA #80+08		;REGULAR MAX PLUS DELTA
	STA WG.FCZ		;BRITENESS OF CLOSEST FORCE-FIELD
				;---
	LDB M$TX+M.U1		;SETUP POINTER TO PANEL FLAGS
	LSRB
	LSRB
	LSRB
	ANDB #0F
	LDX #PNLW		;FLAG ARRAY
	ABX
	STX S2.PRM
				;---
	LDA M$TX+M.U1		;CURRENT SHIP POSITION
	ANDA #0F8
	ADDA #4			;THEN GET INTO MIDDLE OF PANEL
	LDB #0
	STD M.X0
	LDD #-400
	STD M.Y0
	BEGIN			;---LOOP FOR EACH SEGMENT
	LDD #-0E00		;BASE START(MID OF LOWEST SEGMENT)
	STD M.Z0
	LDX S2.PRM
	CMPX #PNLWZ
	IFHS			;?WRAPAROUND?
	LEAX -PNLW$IZ(X)	;MOVE BACK MOD(WHOLE TABLE)
	ENDIF
	LDA (X)+		;GET FLAGS
	STX S2.PRM
;;;	TSTA			;STX TO KEEP LATEST POINTER, TSTA TO GET VAL
	BEGIN			;---LOOP EACH PANEL
	STA PN.FLG		;WORKING FLAG BITS
	ANDA #3
	CMPA PN.DES		;CHECK AGAINST DESIRED PANEL TYPE
	LBNE 50$			;?CORRECT TYPE?
				;---
	
	LDA PN.ID
	STA BJ.ID		;PREPARE CORRECT OBJECT IDENTIFICATION
	LDD #M$X0BC
	STD M.BIC
	LDA #M$PSB2
	JSR MGOWT		;GET PANELS CENTER POINT OUT THERE
				;---
	LDA PN.DES
	CMPA #2
	IFEQ			;?GOT A FORCE FIELD TO COLOR CUE?
	INC WG.FCB		;BUMP OUR COLOR CUE FOR LATER PANEL
	LDB WG.FCC
	LSLB
	LDX #TFFCUE		;COLOR CUE THE WEDGES FOR DEPTH VISION
	LDD B(X)
	STD (Y)++
	LDA WG.FCZ		;Z-BRITENESS TO USE
	STA -1(Y)		;OVERLAY PREVIOUS BRIGHTNESS
	ENDIF
				;---
	LDD M.X0
	SUBD M$TX+M.U1		;GET RELATIVE DISP TO PANEL CENTER
	CMPD #1000
	IFLE
	LDA PN.ID
	CMPA #TD$WFF
	IFEQ			;?DOING A FORCE FIELD?
	LDD M.XP		;?FRONT PART VISIBLE?
	SUBD #0
	BLT 50$			;B EXIT IF NOT REALLY VISIBLE
	LDD M$TY+M.U1		;THEN CHECK LEFT/RIGHT
	IFLE			;?ON LEFT SIDE?
	LDD M.Z0
	ADDD #200		;TOP OF FORCE FIELD
	SUBD M$TZ+M.U1
	IFGE			;?FORCE FIELD ABOVE PLAYER?
	SUBD #400
	IFLE			;?BUT NOT TOO FAR?
	LDD M.X0
	SUBD M$TX+M.U1
	SUBD #400		;HALF OF FF DEPTH, MORE THAN MAX SPEED
	IFLS			;?WITHIN FIRST HALF OF FORCE FIELD?
	LDA #TD$WFG		;THEN A HIT, CHANGE THE PICTURE TO BRITE
	STA BJ.ID
	LDA S.GAS
	IFGE			;?STILL IN NORMAL GAMEPLAY?
	JSR BG1GLW		;BEGIN GLOWING PLAYERS SHIP
	JSR AUDCR		;CRASH INTO FORCE FIELD
	ENDIF
	LDA S.ROL
	IFEQ			;?FINISHED WITH PRIOR COLLIDE?
	LDA #78.
	LDB P.RND1
	IFMI
	NEGA			;FLIP OTHER DIRECTION FOR VARIETY
	ENDIF
	STA S.ROL
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	JSR BJBCLP		;ALL CLOSE PANELS GET POINTS CLIPPED
	ELSE			;ELSE
	JSR BJGPNT		;DO POINTS SPEEDILY
	ENDIF
				;---
	JSR BJGDRW
50$:
	LDD M.Z0
	ADDD #400		;MOVE UP TO NEXT PANEL
	STD M.Z0
	LDA PN.FLG
	LSRA
	LSRA
	EQEND			;LOOP TO NEXT PANEL UP
				;---
	LDA PN.DES
	CMPA #02
	IFEQ
	LDX S2.PRM		;ONE PAST CURRENT PANNEL
	LDA PNRW-PNLW-1(X)	;POINT TO OTHER PANNEL SET
	BEGIN
	LSLA
	IFCS			;2 OR 3?
	IFPL			;MUST BE A 2 ( FORCE FIELD )
	INC WG.FCB		;BUMP OUR COLOR CUE
	BRA 60$
	ENDIF
	ENDIF
	LSLA
	EQEND
60$:
				;---
	LDA WG.FCB
	IFNE			;?HAVE AT LEAST ONE FORCE FIELD IN THIS PANEL?
	LDA WG.FCC
	INCA
	CMPA #ZFFCUE-TFFCUE/2
	IFHS			;?OVERFLOW TABLE SIZE?
	LDA #0
	ENDIF
	STA WG.FCC		;CURRENT COLOR CUE INDEX
				;---
	LDA WG.FCZ		;CURRENT BRITENESS
	SUBA #08		;FURTHER PANELS ARE DIMMER
	CMPA #40		;LOWER LIMIT
	IFLO			;?TOO DIM? OR WRAPPED?
	LDA #40
	ENDIF
	STA WG.FCZ		;SAVE NEXT BRITENESS
				;---
	LDA #0
	STA WG.FCB
	ENDIF
	ENDIF
				;---
	LDD M.X0
	ADDD #800		;GET NEXT SEGMENT
	STD M.X0
	SUBD M$TX+M.U1		;GET RELATIVE DISPLACEMENT
	SUBD #7000		;?WITHIN VIEW?
	HSEND			;---LOOP NEXT SEGMENT

	RTS
				;===
PNVRW:				;VIEW THE LEFT PANELS
	LDA #0
	STA WG.FCB		;NO BUMP TO NEXT FORCE FIELD CUE YET
	LDA WG.FCU		;INIT CURRENT CUE COLOR
	STA WG.FCC
	LDA #80+08		;MAX BRITE PLUS DELTA
	STA WG.FCZ		;STARTING BRITENESS FOR NEAREST FORCE FIELD
				;---
	LDB M$TX+M.U1		;SETUP POINTER TO PANEL FLAGS
	LSRB
	LSRB
	LSRB
	ANDB #0F
	LDX #PNRW		;FLAG ARRAY
	ABX
	STX S2.PRM
				;---
	LDA M$TX+M.U1		;CURRENT SHIP POSITION
	ANDA #0F8
	ADDA #4			;THEN GET INTO MIDDLE OF PANEL
	LDB #0
	STD M.X0
	LDD #400
	STD M.Y0
	BEGIN			;---LOOP FOR EACH SEGMENT
	LDD #-0E00		;BASE START(MID OF LOWEST SEGMENT)
	STD M.Z0
	LDX S2.PRM
	CMPX #PNRWZ
	IFHS			;?WRAPAROUND?
	LEAX -PNRW$IZ(X)	;MOVE BACK MOD(WHOLE TABLE)
	ENDIF
	LDA (X)+		;GET FLAGS
	STX S2.PRM
;;;	TSTA			;STX TO KEEP LATEST POINTER, TSTA TO GET VAL
	BEGIN			;---LOOP EACH PANEL
	STA PN.FLG		;WORKING FLAG BITS
	ANDA #3
	CMPA PN.DES		;CHECK AGAINST DESIRED PANEL TYPE
	LBNE 50$		;?CORRECT TYPE?
				;---
	LDA PN.ID
	STA BJ.ID		;SETUP CORRECT OBJECT ID
	LDD #M$X0BC
	STD M.BIC
	LDA #M$PSB2
	JSR MGOWT		;GET PANELS CENTER POINT OUT THERE
				;---
	LDA PN.DES
	CMPA #2
	IFEQ			;?GOT A FORCE FIELD TO COLOR CUE?
	INC WG.FCB		;BUMP OUR COLOR CUE FOR LATER PANEL
	LDB WG.FCC
	LSLB
	LDX #TFFCUE		;COLOR CUE THE WEDGES FOR DEPTH VISION
	LDD B(X)
	STD (Y)++
	LDA WG.FCZ
	STA -1(Y)		;OVERLAY PREVIOUS BRIGHTNESS
	ENDIF
				;---
	LDD M.X0
	SUBD M$TX+M.U1		;GET RELATIVE DISP TO PANEL CENTER
	CMPD #1000
	IFLE
	LDA PN.ID
	CMPA #TD$WFF
	IFEQ			;?DOING A FORCE FIELD?
	LDD M.XP		;TEST FOR REALLY IN VIEW
	SUBD #0
	BLT 50$			;B EXIT IF PAST FRONT HALF
	LDD M$TY+M.U1		;THEN CHECK LEFT/RIGHT
	IFGE			;?ON RIGHT SIDE?
	LDD M.Z0
	ADDD #200		;BOTTOM OF FORCE FIELD
	SUBD M$TZ+M.U1
	IFGE			;?BENEATH TOP OF FORCE FIELD?
	SUBD #400
	IFLE			;?AND ABOVE BOTTOM?
	LDD M.X0
	SUBD M$TX+M.U1
	SUBD #400		;HALF OF FF DEPTH, MORE THAN MAX SPEED
	IFLS			;?WITHIN FIRST HALF OF FORCE FIELD?
	LDA #TD$WFG		;THEN A HIT, CHANGE THE PICTURE TO BRITE
	STA BJ.ID
	LDA S.GAS		;?STILL IN NORMAL GAMEPLAY?
	IFGE
	JSR BG1GLW		;BEGIN GLOWING SHIP SHIELDS
	JSR AUDCR		;CRASH INTO FORCE FIELD
	ENDIF
	LDA S.ROL
	IFEQ			;JUST ONE HIT PER COLLISION PLEASE
	LDA #78.
	LDB P.RND1
	IFMI
	NEGA			;FLIP OTHER DIRECTION TO EVEN US OUT
	ENDIF
	STA S.ROL
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	ENDIF
	JSR BJBCLP		;ALL CLOSE PANELS GET POINTS CLIPPED
	ELSE			;ELSE
	JSR BJGPNT		;DO POINTS SPEEDILY
	ENDIF
				;---
	JSR BJGDRW
50$:
	LDD M.Z0
	ADDD #400		;MOVE UP TO NEXT PANEL
	STD M.Z0
	LDA PN.FLG
	LSRA
	LSRA
	EQEND			;LOOP TO NEXT PANEL UP
				;---
	LDA PN.DES
	CMPA #02
	IFEQ
	LDX S2.PRM		;ONE PAST CURRENT PANNEL
	LDA PNLW-PNRW-1(X)	;POINT TO OTHER PANNEL SET
	BEGIN
	LSLA
	IFCS			;2 OR 3?
	IFPL			;MUST BE A 2 ( FORCE FIELD )
	INC WG.FCB		;BUMP OUR COLOR CUE
	BRA 60$
	ENDIF
	ENDIF
	LSLA
	EQEND
60$:
				;---
	LDA WG.FCB
	IFNE			;?HAVE AT LEAST ONE FORCE FIELD IN THIS PANEL?
	LDA WG.FCC
	INCA
	CMPA #ZFFCUE-TFFCUE/2
	IFHS			;?OVERFLOW TABLE SIZE?
	LDA #0
	ENDIF
	STA WG.FCC		;CURRENT COLOR CUE INDEX
				;---
	LDA WG.FCZ		;CURRENT BRITENESS
	SUBA #08		;FURTHER PANELS ARE DIMMER
	CMPA #40		;LOWER LIMIT
	IFLO			;?TOO DIM? OR WRAPPED?
	LDA #40
	ENDIF
	STA WG.FCZ		;SAVE NEXT BRITENESS
				;---
	LDA #0
	STA WG.FCB
	ENDIF
	ENDIF
				;---
	LDD M.X0
	ADDD #800		;GET NEXT SEGMENT
	STD M.X0
	SUBD M$TX+M.U1		;GET RELATIVE DISPLACEMENT
	SUBD #7000		;?WITHIN VIEW?
	HSEND			;---LOOP NEXT SEGMENT

	RTS
				;===

TFFCUE:
	.WORD VGCYLW*100+VGCOPC
	.WORD VGCTRQ*100+VGCOPC
	.WORD VGCPRP*100+VGCOPC
ZFFCUE:
				;===

	HLL69F
	.END
                                                                                                                          